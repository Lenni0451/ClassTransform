allprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"

    java.toolchain.languageVersion = JavaLanguageVersion.of(8)
    compileJava.options.encoding = "UTF-8"

    group = project.maven_group
    archivesBaseName = project.maven_name
    version = project.maven_version
    if (!project.maven_group) group = rootProject.maven_group
    if (!project.maven_name) archivesBaseName = rootProject.maven_name
    if (!project.maven_version) version = rootProject.maven_version

    java {
        withSourcesJar()
        withJavadocJar()
    }

    repositories {
        mavenCentral()
    }

    publishing {
        repositories {
            maven {
                name = "reposilite"
                url = "https://maven.lenni0451.net/snapshots"
                credentials(PasswordCredentials)
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
        publications {
            maven(MavenPublication) {
                groupId = project.maven_group
                artifactId = project.maven_name
                version = project.maven_version

                from components.java
            }
        }
    }
}

dependencies {
    api "org.ow2.asm:asm:9.4"
    api "org.ow2.asm:asm-commons:9.4"
    api "org.ow2.asm:asm-tree:9.4"

    testImplementation platform("org.junit:junit-bom:5.9.0")
    testImplementation "org.junit.jupiter:junit-jupiter"
}


test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    maxParallelForks Runtime.runtime.availableProcessors()
}

build.dependsOn(test)
